#_( ;; Allow this script to be executed directly
   "exec" "bb" "--classpath" "$(clojure -Spath -Sdeps '{:deps {io.aviso/pretty {:mvn/version "1.1"}}}')" "$0" "$(readlink -f $(dirname $0)/..)" "$@"
   )

;; Copyright Â© 2021, JUXT LTD.

(ns site
  (:require
   [babashka.curl :as curl]
   [clojure.tools.cli :refer [parse-opts summarize]]
   [clojure.java.io :as io]
   [clojure.java.shell :as sh]
   [cheshire.core :as json]
   [clojure.pprint :refer [pprint]]
   [clojure.edn :as edn]
   [io.aviso.ansi :as ansi]
   [clojure.string :as str])
  (:import
   (java.nio.file Files)
   (java.nio.file.attribute FileAttribute)))

(def ^:dynamic *silent* nil)
(def ^:dynamic *debug* nil)

(def site-root-dir (io/file (first *command-line-args*)))

(defn edn-to-json
  [edn]
  (-> (edn/read-string {:readers {'juxt.site.alpha/as-str pr-str}} edn)
      (json/encode)))

(defn error! [& msg]
  (when-not *silent*
    (apply println (apply str ansi/red-font "fail:" ansi/reset-font) msg)))

(defn error-and-exit! [exit & msg]
  (apply error! msg)
  (System/exit exit))

(defn error-and-usage! [msg opts]
  (error! msg)
  (println "Usage:")
  (println (:summary opts))
  (System/exit 1))

(defn message! [& msg]
  (when-not *silent*
    (apply println (apply str ansi/bold-green-font "site:" ansi/reset-font) msg)))

(def config-file
  (case (System/getProperty "os.name")
    ("Linux" "Mac OS X") (io/file (System/getProperty "user.home") ".config/site/config.edn")
    (error-and-exit! 1 "Unsupported OS")))

(defn file-extension
  [path]
  (subs path (inc (str/last-index-of path "."))))

;;(assert (.exists config-file) (format "Config file does not exist: %s" (str config-file)))

(def config
  (cond-> {}
    (.exists config-file)
    (merge
     (edn/read-string
      {:readers {'ig/ref identity
                 'ref identity
                 'profile #(get % :prod)
                 'env #(System/getenv (str %))
                 'join #(apply str %)}}
      (slurp config-file)))
    (System/getenv "SITE_BASE_URI")
    (assoc :juxt.site.alpha/base-uri (System/getenv "SITE_BASE_URI"))))

(defn get-password-from-unix-pass [unix-pass-password-prefix username]
  (let [passfile (io/file
                  (System/getProperty "user.home")
                  (str ".password-store/" unix-pass-password-prefix username ".gpg"))]
    (if (.exists passfile)
      (do
        (message! "Extracting password from" (str passfile))
        (let [{:keys [exit out]} (sh/sh "gpg" "-d" (str passfile))]
          (when (zero? exit) (str/trim out))))
      (error-and-exit! 1 (format "Passfile does not exist: %s" passfile)))))

(defn get-password [unix-pass-password-prefix username]
  (or
   (get-password-from-unix-pass unix-pass-password-prefix username)
   ;; TODO: Try other strategies, like pinentry
   (error-and-exit! 1 "Failed to get password for" username)))

(defn local-token-file []
  (let [dir (io/file (System/getProperty "user.home") ".local/share/site")]
    (when-not (.exists dir)
      (when-not (.mkdirs dir)
        (error-and-exit! 1 (str "Failed to make directory: " dir))))
    (io/file dir "access-token.json")))

(defn show-config
  "Show configuration"
  [opts]
  (pprint config))

(defn get-password-prefix []
  (or
   (when-let [dir (System/getenv "SITE_PASS_DIR")]
     (cond-> dir
       (not (.endsWith dir "/")) (str "/")))
   (:juxt.site.alpha.unix-pass/password-prefix config)))

(defn get-token
  "Request a token"
  [opts]
  (let [{:keys [base-uri username password password-prefix curl]
         :or {base-uri (:juxt.site.alpha/base-uri config)
              password-prefix (get-password-prefix)
              username (System/getProperty "user.name")}}
        (:options opts)]

    (when password-prefix
      (message! (format "Unix pass prefix: %s" password-prefix)))

    (when-not base-uri (error-and-exit! 1 "Base-URI not specified"))
    (when-not username (error-and-exit! 1 "Username required"))

    (message! "Get token from" (format "%s/_site/token" base-uri))

    (let [{:keys [status headers body]}
          (curl/post
           (format "%s/_site/token" base-uri)
           {:headers {"content-type" "application/x-www-form-urlencoded"}
            :basic-auth [username (or password
                                      (when password-prefix (get-password password-prefix username))
                                      (error-and-exit! 1 "Failed to get password"))]
            :body "grant_type=client_credentials"
            :throw false
            :debug false})]
      (cond
        (= status 401)
        (do
          (message! "Server response:" status body)
          (message! (format "Are your credentials valid? request-id is %s" (get headers "site-request-id"))))

        (= status 200)
        (let [f (local-token-file)
              data (json/decode body)
              expires (java.util.Date.
                       (+ (.getTime (java.util.Date.))
                          (* (get data "expires_in") 1000)))

              data (-> data
                       (assoc "username" username)
                       (assoc "expires" (.format java.time.format.DateTimeFormatter/ISO_INSTANT (.toInstant expires))))]

          (message! (format "Token expires in %s seconds" (get data "expires_in")))
          (message! (format "Storing token in %s" f))
          (spit f (json/encode data {:pretty true}))
          (when curl
            (message! (format "Storing curl config in %s" curl))
            (spit curl (format "-H \"Authorization: Bearer %s\"\n" (get data "access_token")))))

        :else
        (do
          (println "Server response:" status (str/trim body)))))))

(defn load-token []
  "Load token from local cache"
  (let [f (local-token-file)
        _ (when-not (.exists f)
            (error-and-exit! 1 "Please request a token with the 'get-token' subcommand"))
        {access-token "access_token" expires "expires" :as token}
        (json/decode (slurp f))]

    (if (.isBefore (java.time.Instant/parse expires) (java.time.Instant/now))
      (error-and-exit! 1 (format "Access token has expired. Request a new token with %ssite get-token%s" ansi/cyan-font ansi/reset-font))
      token)))

(defn check-token
  "Check the validity of the locally cached token"
  [_]
  (let [local-token-file (local-token-file)]

    (if-not (.exists local-token-file)
      (error-and-exit! 1 (format "Access token does not exist. Request a token with %ssite get-token%s" ansi/cyan-font ansi/reset-font))
      (let [token (json/decode (slurp local-token-file))
            expires (get token "expires")]

        (if (.isBefore (java.time.Instant/parse expires) (java.time.Instant/now))
          (error-and-exit! 1 "Access token has expired")
          (let [mins-left (.until (java.time.Instant/now)
                                  (java.time.Instant/parse expires)
                                  java.time.temporal.ChronoUnit/MINUTES)]
            (if (> mins-left 5)
              (message! (format "Access token expires in %d minutes" mins-left))
              (error-and-exit! 2 (format "Access token expires in %d seconds and should be refreshed"
                                         (.until (java.time.Instant/now)
                                                 (java.time.Instant/parse expires)
                                                 java.time.temporal.ChronoUnit/SECONDS))))))))))

(defn clear-token
  "Clear the token to end the session"
  [_]
  (if (.delete (local-token-file))
    (message! "Token deleted")
    (when (.exists (local-token-file))
      (error! "Failed to delete token"))))

(defn list-users
  "List users"
  [opts]
  (let [{:keys [base-uri]
         :or {base-uri (:juxt.site.alpha/base-uri config)}}
        (:options opts)]
    (let [{access-token "access_token"} (load-token)
          {:keys [status headers body]}
          (curl/get
           (format "%s/_site/users/" base-uri)
           {:headers {"authorization" (format "Bearer %s" access-token)
                      "accept" "application/json"}
            :throw false})]
      (cond
        (= (quot status 100) 2)
        (println (json/encode (json/decode body) {:pretty true}))

        :else
        (do
          (error! "Server response:" status (str/trim body))
          (message! "Request id:" (get headers "site-request-id")))))))

(defn reset-password
  "Reset a user's password"
  [opts]
  (let [{access-token "access_token"} (load-token)
        {:keys [base-uri username password]
         :or {base-uri (:juxt.site.alpha/base-uri config)}}
        (:options opts)
        password-prefix (get-password-prefix)]

    (when-not base-uri (error-and-exit! 1 "Base-URI required"))
    (when-not username (error-and-usage! "User required" opts))

    (let [password (cond
                     password password
                     password-prefix (get-password password-prefix username))

          _ (when-not password
              (error-and-exit! 1 "No password to set"))

          {:keys [status headers body]}
          (curl/put
           (format "%s/_site/users/%s/password" base-uri username)
           {:headers {"authorization" (format "Bearer %s" access-token)
                      "content-type" "application/json"}
            :body (json/generate-string
                   {"type" "Password"
                    "user" (format "%s/_site/users/%s" base-uri username)
                    "password" password})
            :throw false})]
      (cond
        (= status 401)
        (error-and-exit! 1 "Server response: " status " " (str/trim body))

        (= (quot status 100) 2)
        (message! "Password reset for" username)

        :else
        (do
          (error! "Server response: " status " " (str/trim body))
          (message! "Request id:" (get headers "site-request-id")))))))

(defn put-user
  "Create or update a user"
  [opts]
  (let [{access-token "access_token"} (load-token)
        {:keys [base-uri username password name email]
         :or {base-uri (:juxt.site.alpha/base-uri config)}}
        (:options opts)]

    (when-not base-uri (error-and-exit! 1 "Base-URI not specified"))
    (when-not username (error-and-usage! "Username required" opts))
    (when-not name (error-and-usage! "Name required" opts))
    (when-not email (error-and-usage! "Email required" opts))

    (let [{:keys [status headers body]}
          (curl/put
           (format "%s/_site/users/%s" base-uri username)
           {:headers {"authorization" (format "Bearer %s" access-token)
                      "content-type" "application/json"}
            :body (json/generate-string
                   {"type" "User"
                    ;;"username" username
                    "name" name
                    "email" email})
            :throw false})]
      (cond
        (= status 401)
        (do
          (error! "Server response: " status " " (str/trim body))
          (message! "Is your access-token valid?")
          (message! "To get a new access token, use the get-token sub-command")
          (message! "Request id: " (get headers "site-request-id")))

        (= status 201)
        (message! "User created:" (format "%s (Welcome %s!)" username name))

        (= status 204)
        (message! "User updated:" username)

        :else
        (do
          (error! "Server response: " status " " (str/trim body))
          (message! "Request id:" (get headers "site-request-id"))))

      (when (#{201 204} status) (reset-password opts)))))

(defn put-role
  "Create or update a role"
  [opts]
  (let [{access-token "access_token"} (load-token)
        {:keys [base-uri role description]
         :or {base-uri (:juxt.site.alpha/base-uri config)}}
        (:options opts)]

    (when-not base-uri (error-and-exit! 1 "Base-URI not specified"))
    (when-not role (error-and-usage! "Role required" opts))

    (let [{:keys [status headers body]}
          (curl/put
           (format "%s/_site/roles/%s" base-uri role)
           {:headers {"authorization" (format "Bearer %s" access-token)
                      "content-type" "application/json"}
            :body (json/generate-string
                   (cond-> {"type" "Role"
                            "name" role}
                     description (assoc "description" description)))

            :throw false})]
      (cond
        (= status 401)
        (do
          (error! "Server response: " status " " (str/trim body))
          (message! "Is your access-token valid?")
          (message! "To get a new access token, use the get-token sub-command")
          (message! "Request id:" (get headers "site-request-id")))

        (= status 201)
        (message! "Role created:" role)

        (= status 204)
        (message! "Role updated:" role)

        :else
        (do
          (error! "Server response: " status " " (str/trim body))
          (message! "Request id:" (get headers "site-request-id")))))))

(defn list-roles
  "List roles"
  [opts]
  (let [{:keys [base-uri]
         :or {base-uri (:juxt.site.alpha/base-uri config)}}
        (:options opts)]
    (let [{access-token "access_token"} (load-token)
          {:keys [status headers body]}
          (curl/get
           (format "%s/_site/roles/" base-uri)
           {:headers {"authorization" (format "Bearer %s" access-token)
                      "accept" "application/json"}
            :throw false})]
      (cond
        (= (quot status 100) 2)
        (println (json/encode (json/decode body) {:pretty true}))

        :else
        (do
          (error! "Server response: " status " " (str/trim body))
          (message! "Request id:" (get headers "site-request-id")))))))

(defn assign-role
  "Assign a role to a user"
  [opts]
  (let [{access-token "access_token" assigner "username"} (load-token)
        {:keys [base-uri username role justification]
         :or {base-uri (:juxt.site.alpha/base-uri config)}}
        (:options opts)]

    (when-not username (error-and-usage! "Username required" opts))
    (when-not role (error-and-usage! "Role required" opts))

    (let [{:keys [status headers body]}
          (curl/put
           (format "%s/_site/roles/%s/users/%s" base-uri role username)
           {:headers {"authorization" (format "Bearer %s" access-token)
                      "content-type" "application/json"}
            :body (json/generate-string
                   (cond-> {"type" "UserRoleMapping"
                            "assignee" (format "%s/_site/users/%s" base-uri username)
                            "assigner" (format "%s/_site/users/%s" base-uri assigner)
                            "role" (format "%s/_site/roles/%s" base-uri role)}
                     justification (assoc "businessJustification" justification)))
            :throw false})]
      (cond
        (= status 401)
        (do
          (error! "Server response: " status " " (str/trim body))
          (message! "Is your access-token valid?")
          (message! "To get a new access token, use the get-token sub-command")
          (message! "Request id:" (get headers "site-request-id")))

        (= status 201)
        (message! "Assignment created:" username "assigned role of" role)

        (= status 204)
        (message! "Assignment updated:" username "re-assigned role of" role)

        :else
        (do
          (error! "Server response:" status (str/trim body))
          (message! "Request id:" (get headers "site-request-id")))))))

(defn put-rule
  "Put a rule"
  [opts]
  (let [{access-token "access_token"} (load-token)
        {:keys [base-uri name rule]
         :or {base-uri (:juxt.site.alpha/base-uri config)}}
        (:options opts)]

    (when-not base-uri (error-and-exit! 1 "Base-URI not specified"))
    (when-not name (error-and-usage! "Name required" opts))
    (when-not rule (error-and-usage! "Rule required" opts))
    (when-not (.isFile (io/file rule)) (error-and-exit! 1 "Rule file must be a file"))

    (let [{:keys [status headers body]}
          (curl/put
           (format "%s/_site/rules/%s" base-uri name)
           {:headers {"authorization" (format "Bearer %s" access-token)
                      "content-type" "application/json"}
            :body (cond-> (slurp (io/file rule))
                    :always (str/replace "{{base-uri}}" base-uri)
                    (re-matches #".*\.edn$" rule) edn-to-json)
            :throw false})]
      (cond
        (= status 201)
        (message! (format "Rule created: %s" (get headers "location")))

        (= status 204)
        (message! (format "Rule updated: %s" (get headers "location")))

        :else
        (do
          (error! "Server response:" status (str/trim body))
          (message! "Request id:" (get headers "site-request-id")))))))

(defn put-trigger
  "Put a trigger"
  [opts]
  (let [{access-token "access_token"} (load-token)
        {:keys [base-uri name trigger]
         :or {base-uri (:juxt.site.alpha/base-uri config)}}
        (:options opts)]

    (when-not base-uri (error-and-exit! 1 "Base-URI not specified"))
    (when-not name (error-and-usage! "Name required" opts))
    (when-not trigger (error-and-usage! "Trigger file required" opts))
    (when-not (.isFile (io/file trigger)) (error-and-exit! 1 "Trigger file must be a file"))

    (let [{:keys [status headers body]}
          (curl/put
           (format "%s/_site/triggers/%s" base-uri name)
           {:headers {"authorization" (format "Bearer %s" access-token)
                      "content-type" "application/json"}
            :body (cond-> (slurp (io/file trigger))
                    base-uri (str/replace "{{base-uri}}" base-uri)
                    (re-matches #".*\.edn$" trigger)
                    edn-to-json)
            :throw false})]
      (cond
        (= status 201)
        (message! (format "Trigger created: %s" (get headers "location")))

        (= status 204)
        (message! (format "Trigger updated: %s" (get headers "location")))

        :else
        (do
          (error! "Server response:" status (str/trim body))
          (message! "Request id:" (get headers "site-request-id")))))))

(defn put-graphql
  "Put a GraphQL document"
  [opts]
  (let [{access-token "access_token"} (load-token)
        {:keys [base-uri file path]
         :or {base-uri (:juxt.site.alpha/base-uri config)}}
        (:options opts)]

    (when-not base-uri (error-and-exit! 1 "Base-URI not specified"))
    (when-not file (error-and-usage! "GraphQL schema file required" opts))
    (when-not (.isFile (io/file file)) (error-and-exit! 1 "GraphQL schema file must be a file"))
    (when-not path (error-and-usage! "Path required" opts))

    (let [{:keys [status headers body]}
          (curl/put
           (format "%s%s" base-uri path)
           {:headers {"authorization" (format "Bearer %s" access-token)
                      "content-type" "application/graphql"
                      "accept" "text/plain"}
            :body (cond-> (slurp (io/file file))
                    base-uri (str/replace "{{base-uri}}" base-uri))
            :throw false})]
      (cond
        (#{201 204} status)
        (message! (format "GraphQL document uploaded: %s%s %s->%s %s%s"
                          ansi/blue-font (.getName file) ansi/reset-font
                          ansi/blue-font path ansi/reset-font))

        :else
        (do
          (error! "Server response:" status (str/trim body))
          (message! "Request id:" (get headers "site-request-id")))))))

(defn post-graphql
  "Post a GraphQL schema"
  [opts]
  (let [{access-token "access_token"} (load-token)
        {:keys [base-uri file path]
         :or {base-uri (:juxt.site.alpha/base-uri config)}}
        (:options opts)]

    (when-not base-uri (error-and-exit! 1 "Base-URI not specified"))
    (when-not file (error-and-usage! "GraphQL schema file required" opts))
    (when-not (.isFile (io/file file)) (error-and-exit! 1 "GraphQL schema file must be a file"))
    (when-not path (error-and-usage! "Path required" opts))

    (let [{:keys [status headers body]}
          (curl/post
           (format "%s%s" base-uri path)
           {:headers {"authorization" (format "Bearer %s" access-token)
                      "content-type" "application/json"
                      "accept" "text/plain"}
            :body (json/encode {"query" (cond-> (slurp (io/file file))
                                         base-uri (str/replace "{{base-uri}}" base-uri))})
            :throw false})]
      (cond
        (= status 200)
        (do
          (message! (format "GraphQL posted"))
          (message! (str/trim body)))

        :else
        (do
          (error! "Server response:" status (str/trim body))
          (message! "Request id:" (get headers "site-request-id")))))))

(defn put-redirect
  "Put a redirect"
  [opts]
  (let [{access-token "access_token"} (load-token)
        {:keys [base-uri resource location]
         :or {base-uri (:juxt.site.alpha/base-uri config)}}
        (:options opts)]

    (when-not base-uri (error-and-exit! 1 "Base-URI not specified"))
    (when-not resource (error-and-usage! "Resource required" opts))
    (when-not location (error-and-usage! "Location required" opts))

    (let [{:keys [status headers body]}
          (curl/post
           (format "%s/_site/redirects/" base-uri)
           {:headers {"authorization" (format "Bearer %s" access-token)
                      "content-type" "application/json"}
            :body (json/generate-string
                   (cond-> {"type" "Redirect"
                            "resource" (str base-uri resource)
                            "location" (str base-uri location)}))
            :throw false})]
      (cond
        (= status 401)
        (do
          (error! "Server response:" status (str/trim body))
          (message! "Is your access-token valid?")
          (message! "To get a new access token, use the get-token sub-command")
          (message! "Request id:" (get headers "site-request-id")))

        (= status 201)
        (message! (format "Redirect created"))

        (= status 204)
        (message! (format "Redirect updated"))

        :else
        (do
          (error! "Server response:" status (str/trim body))
          (message! "Request id:" (get headers "site-request-id")))))))

(defn put-api
  "Upload and install an API"
  [opts]
  (let [{access-token "access_token"} (load-token)
        {:keys [base-uri name openapi]
         :or {base-uri (:juxt.site.alpha/base-uri config)}} (:options opts)

        _ (when-not name (error-and-usage! "API name required" opts))
        _ (when-not (re-matches #"[a-z]{2,}" name)
            (error-and-usage! "Name must match pattern: [a-z]{2,}" opts))

        _ (when-not openapi (error-and-usage! "OpenAPI file required" opts))
        _ (when-not (.exists (io/file openapi)) (error-and-exit! 1 "OpenAPI file not found"))
        _ (when-not (.isFile (io/file openapi)) (error-and-exit! 1 "OpenAPI argument is not a file"))

        openapi-doc (edn/read-string
                     {:readers {'juxt.site.alpha/as-str pr-str}}
                     (-> (slurp (io/file openapi))
                         (str/replace "{{base-uri}}" base-uri)))

        {:keys [status headers body]}
        (curl/put
         (format "%s/_site/apis/%s/openapi.json" base-uri name)
         {:headers {"authorization" (format "Bearer %s" access-token)
                    "content-type" "application/vnd.oai.openapi+json;version=3.0.2"}
          :body (json/generate-string
                 {"type" "OpenAPI"
                  "openapi" openapi-doc})
          :throw true})]
    (cond
      (#{201 204} status)
      (message! (format "API %s: %s (version %s)"
                        (case status 201 "installed" 204 "updated")
                        (get-in openapi-doc [:info :title])
                        (get-in openapi-doc [:info :version])))

      :else
      (do
        (error! "Server response:" status (str/trim body))
        (message! "Request id:" (get headers "site-request-id"))))))

(defn sh [& args]
  (let [{:keys [exit out err]} (apply sh/sh args)]
    (when (pos? exit)
      (error! err))))

(defn create-tmp-dir []
  (let [tmpdir (Files/createTempDirectory
                "site-" (into-array FileAttribute []))]
    (message! "Created tmp dir:" (.toString tmpdir))
    tmpdir))

(defn infer-content-type
  [extension]
  (case extension
    ("htm" "html") "text/html;charset=utf-8"
    "js" "text/javascript"
    ("map" "json") "application/json"
    "edn" "application/edn"
    "csv" "text/csv"
    "png" "image/png"
    ("jpg" "jpeg") "image/jpeg"
    "svg" "image/svg+xml"
    "ico" "image/x-icon"
    ("gif" "webp" "bmp") (str "image/" extension)
    "css" "text/css; charset=utf-8"
    "txt" "text/plain"
    ("woff" "woff2" "otf" "ttf") (str "font/" extension)
    ("mp4" "mpeg" "webm") (str "video/" extension)
    ("opus" "wav" "aac") (str "audio/" extension)
    "mp3" "audio/mpeg"
    nil))

(defn content-type [filename]
  (when-let [suffix (file-extension filename)]
    (or
     (infer-content-type suffix)
     (do
       (error! "Failed to determine content-type for"
               filename (format "(suffix=%s)" suffix)
               " Falling back to application/octet-stream")
       "application/octet-stream"))))

(defn upload-file* [access-token base-uri file type encoding language path opts]
  (assert (instance? java.io.File file))
  (when-not (.exists file)
    (error-and-exit! 1 (format "Cannot upload file that does not exist: %s" (str file))))

  (message! "Uploading file to" (str base-uri path))

  (let [variant-of (some-> (get-in opts [:options :resource])
                           (str/replace "{{base-uri}}" base-uri))
        classification (get-in opts [:options :classification])
        dialect (get-in opts [:options :dialect])
        {:keys [status headers body]}
        (curl/put
         (str base-uri path)
         {:headers (cond-> {"authorization" (format "Bearer %s" access-token)
                            ;; want errors in text/plain, will accept json
                            "accept" "text/plain,application/json;q=0.8"
                            "content-type" type}
                     encoding (assoc "content-encoding" encoding)
                     language (assoc "content-language" language)
                     variant-of (assoc "site-variant-of" variant-of)
                     classification (assoc "site-classification" classification)
                     dialect (assoc "site-template-dialect" dialect))
          :body file
          :throw false})]

    (cond
      (= status 400)
      (do
        (error! "Server response:" status)
        (when body (println (str/trim body)))
        (message! "Request id:" (get headers "site-request-id")))

      (= status 401)
      (do
        (error! "Server response:" status (str/trim body))
        (message! "Is your access-token valid?")
        (message! "To get a new access token, use the get-token sub-command")
        (message! "Request id:" (get headers "site-request-id")))

      (= status 200)
      (do
        (message! (format "Asset uploaded: %s" path))
        (message! "Server response:" status (str/trim body)))

      (= status 201)
      (message! (format "Asset created: %s" path))

      (= status 204)
      (message! (format "Asset updated: %s" path))

      :else
      (do
        (error! "Server response:" status (str/trim body))
        (message! "Request id:" (get headers "site-request-id"))))))

(defn put-asset
  "Upload an asset"
  [opts]
  (let [{access-token "access_token"} (load-token)
        {:keys [base-uri file type encoding language path]
         :or {base-uri (:juxt.site.alpha/base-uri config)}}
        (:options opts)
        type (or type (when file (content-type (.getName file))))]

    (when-not base-uri (error-and-exit! 1 "Base-URI not specified"))
    (when-not file (error-and-usage! "Asset file required" opts))
    (when-not type (error-and-usage! "Content-type required" opts))
    (when-not path (error-and-usage! "Path required" opts))

    (upload-file*
     access-token
     base-uri
     (io/file (str/replace file "{{base-uri}}" base-uri))
     type encoding language path opts)))

(defn put-template
  "Upload an template"
  [opts]
  (let [{access-token "access_token"} (load-token)
        {:keys [base-uri file type encoding language path dialect]
         :or {base-uri (:juxt.site.alpha/base-uri config)}}
        (:options opts)
        type (or type (when file (content-type (.getName file))))]

    (when-not base-uri (error-and-exit! 1 "Base-URI not specified"))
    (when-not file (error-and-usage! "Asset file required" opts))
    (when-not type (error-and-usage! "Content-type required" opts))
    (when-not path (error-and-usage! "Path required" opts))
    (when-not dialect (error-and-usage! "Dialect required" opts))

    (upload-file*
     access-token
     base-uri
     (io/file (str/replace file "{{base-uri}}" base-uri))
     type encoding language path opts)))

(defn post-resources
  "Post one or more resources"
  [opts]
  (let [{access-token "access_token"} (load-token)
        {:keys [base-uri file]
         :or {base-uri (:juxt.site.alpha/base-uri config)}} (:options opts)]
    (when-not base-uri (error-and-exit! 1 "Base-URI not specified"))
    (when-not (.exists (io/file file)) (error-and-exit! 1 (str "File not found: " file)))

    (let [{:keys [status headers body]}
          (curl/post
           (str base-uri "/_site/resources/")
           {:headers (cond-> {"authorization" (format "Bearer %s" access-token)
                              "content-type" "application/edn"})
            :body (cond-> (slurp (io/file file))
                    :always (str/replace "{{base-uri}}" base-uri))
            :throw false})]
      (case status
        (201 204)
        (message! (format "Resource %s: %s"
                          (case status 201 "created" 204 "updated")
                          (get headers "location")))

        207
        (let [results (edn/read-string body)
              errors (filter #(contains? % :error) results)]
          (if (pos? (count errors))
            (do
              (error! (format "Not all resources were accepted:"))
              (dorun (map (fn [ix r]
                            (cond
                              (< (:status r) 400)
                              (println (format "%s%d: %s%s" ansi/green-font ix r ansi/reset-font))
                              :else
                              (println (format "%s%d: %s%s" ansi/red-font ix r ansi/reset-font))))
                          (range) results)))
            (message! (format "Resources all posted OK"))))

        (do
          (error! "Server response:" status (str/trim body))
          (message! "Request id:" (get headers "site-request-id")))))))

(defn post
  "Post a resource"
  [opts]
  (let [{access-token "access_token"} (load-token)
        {:keys [base-uri path url type encoding language data file]
         :or {base-uri (:juxt.site.alpha/base-uri config)}} (:options opts)]
    (when-not base-uri (error-and-exit! 1 "Base-URI not specified"))

    (when-not (or path url)
      (error-and-exit! 1 "PATH or URI required"))

    (when (and path url)
      (error-and-exit! 1 "Must specify one of PATH and URI"))

    (when (and data file)
      (error-and-exit! 1 "Must specify one of DATA and FILE"))

    (when (and file (not (.exists (io/file file))))
      (error-and-exit! 1 (str "File not found: " file)))

    (let [url (if path (str base-uri path) (str/replace url "{{base-uri}}" base-uri))

          type (or
                type
                (when file (content-type (.getName file)))
                (error-and-exit! 1 "Content-type must be specified or deducible from the file name"))

          {:keys [status headers body]}
          (curl/post
           url
           {:headers (cond-> {"authorization" (format "Bearer %s" access-token)
                              "content-type" type}
                       encoding (assoc "content-encoding" encoding)
                       language (assoc "content-language" language))
            :body (or data (slurp (io/file file)))
            :throw false})]
      (case status
        200
        (do
          (message! (format "OK: %s" status))
          (println (str/trim body)))

        (201 204)
        (message! (format "Resource %s: %s"
                          (case status 201 "created" 204 "updated")
                          (get headers "location")))
        (do
          (error-and-exit! 1 "Server response:" status (str/trim body))
          ;;(message! "Request id:" (get headers "site-request-id"))
          )))))

(defn post-json
  "Post JSON to a resource"
  [opts]
  (let [{access-token "access_token"} (load-token)
        {:keys [base-uri file url]
         :or {base-uri (:juxt.site.alpha/base-uri config)}} (:options opts)]
    (when-not base-uri (error-and-exit! 1 "Base-URI not specified"))
    (when-not file (error-and-exit! 1 "File required"))
    (when-not (.exists (io/file file)) (error-and-exit! 1 (str "File not found: " file)))
    (when-not url (error-and-exit! 1 "URL is required"))

    (let [url (str/replace url "{{base-uri}}" base-uri)
          {:keys [status headers body]}
          (curl/post
           url
           {:headers (cond-> {"authorization" (format "Bearer %s" access-token)
                              "content-type" "application/json"})
            :body (cond-> (slurp (io/file file))
                    :always (str/replace "{{base-uri}}" base-uri)
                    (.endsWith file ".edn") (edn-to-json))
            :throw false})]
      (case status
        (201 204)
        (message! (format "Resource %s: %s"
                          (case status 201 "created" 204 "updated")
                          (get headers "location")))
        (do
          (error! "Server response:" status (str/trim body))
          (message! "Request id:" (get headers "site-request-id")))))))

(defn put-json
  "PUT JSON to a resource"
  [opts]
  (let [{access-token "access_token"} (load-token)
        {:keys [base-uri file url]
         :or {base-uri (:juxt.site.alpha/base-uri config)}} (:options opts)]
    (when-not base-uri (error-and-exit! 1 "Base-URI not specified"))
    (when-not file (error-and-exit! 1 "File required"))
    (when-not (.exists (io/file file)) (error-and-exit! 1 (str "File not found: " file)))

    (let [embedded-url (when (.endsWith file ".edn")
                         (:xt/id (edn/read-string (slurp file))))

          _ (when (and embedded-url url) (error-and-exit! 1 "Should not specify URL both in the document and as an option"))

          url (or embedded-url url)

          _ (when-not url (error-and-exit! 1 "Failed to specify identity (URI), either in the document (xt/id) or as an option"))

          url (str/replace url "{{base-uri}}" base-uri)
          {:keys [status headers body]}
          (curl/put
           url
           {:headers (cond-> {"authorization" (format "Bearer %s" access-token)
                              "content-type" "application/json"})
            :body (cond-> (slurp (io/file file))
                    :always (str/replace "{{base-uri}}" base-uri)
                    (.endsWith file ".edn") (edn-to-json))
            :throw false})]
      (case status
        (201 204)
        (message! (format "Resource %s: %s"
                          (case status 201 "created" 204 "updated")
                          (get headers "location")))
        (do
          (error! "Server response:" status (str/trim body))
          (message! "Request id:" (get headers "site-request-id")))))))

(defn put-static-site
  "Given a path and a directory containing at least an index.html file, serves the
  files in the directory using the given path as the root"
  [opts]
  (try
    (let [{access-token "access_token"} (load-token)
          {:keys [base-uri directory path]
           :or {base-uri (:juxt.site.alpha/base-uri config)}} (:options opts)]
      (message! "putting site" base-uri directory path)
      (when-not base-uri (error-and-exit! 1 "Base-URI not specified"))
      (when-not (some->> (str directory "/index.html")
                         io/file
                         .exists)
        (error-and-exit! 1 (str "No index.html found in directory: " directory)))
      (let [base (str (.getPath (.toURI (io/file (System/getProperty "user.dir")))) directory)
            relative-path (fn [file] (str path (str/replace-first (.getPath (.toURI file)) base "")))
            put-file
            (fn [file]
              (let [destination (str base-uri "/" (relative-path file))
                    _ (message! (str "Putting file " (.getAbsolutePath file) " to path " destination))
                    {:keys [status headers body]}
                    (curl/put
                     destination
                     {:headers {"authorization" (format "Bearer %s" access-token)
                                "content-type" (content-type (.getName file))}
                      :body (io/file file)
                      :throw false})]
                (case status
                  (201 204)
                  (message! (format "Resource %s: %s"
                                    (case status 201 "created" 204 "updated")
                                    (get headers "location")))

                  207
                  (let [results (edn/read-string body)
                        errors (filter #(contains? % :error) results)]
                    (if (pos? (count errors))
                      (do
                        (error! (format "Not all resources were accepted:"))
                        (dorun (map (fn [ix r]
                                      (cond
                                        (< (:status r) 400)
                                        (println (format "%s%d: %s%s" ansi/green-font ix r ansi/reset-font))
                                        :else
                                        (println (format "%s%d: %s%s" ansi/red-font ix r ansi/reset-font))))
                                    (range) results)))
                      (message! (format "Resources all posted OK"))))

                  (do
                    (error! "Server response:" status (str/trim body))
                    (message! "Request id:" (get headers "site-request-id"))))))]
        (some->> directory
                 io/file
                 file-seq
                 (filter #(.isFile %))
                 (mapv put-file))
        (message! "Site successfully deployed. Visit "
                  (str base-uri "/" (-> directory
                                        (str "/index.html")
                                        io/file
                                        relative-path)))))
    (catch Exception e (str "error putting static site " e))))

(defn debugf
  "Write a debug message to the console, but only if *debug* is true."
  [& args]
  (when *debug*
    (println "DEBUG:" (apply format args))))

(def global-cli-options
  [["-b" "--base-uri BASE-URI" "The Base-URI of the server you are targetting"]
   ["-s" "--silent" "Silent mode"]
   ["-D" "--debug" "Enable debug mode"]
   ["-v" "--verbose" "Verbose mode"]
   ["-h" "--help" "Show this summary"]])

(def commands
  [["config"
    {:description (:doc (meta #'show-config))
     :cli-options []
     :delegate show-config}]

   ["get-token"
    {:description (:doc (meta #'get-token))
     :cli-options [["-u" "--username USERNAME" "Username"]
                   ["-p" "--password PASSWORD" "Password"]
                   ["-K" "--curl FILE" "Store creds in curl config file"]]
     :delegate get-token}]

   ["check-token"
    {:description (:doc (meta #'check-token))
     :cli-options []
     :delegate check-token}]

   ["clear-token"
    {:description (:doc (meta #'clear-token))
     :cli-options []
     :delegate clear-token}]

   ["list-users"
    {:description (:doc (meta #'list-users))
     :cli-options []
     :delegate list-users}]

   ["put-user"
    {:description (:doc (meta #'put-user))
     :cli-options [["-n" "--name NAME" "The user's name"]
                   ["-u" "--username USERNAME" "The username of the user you are creating/updating"]
                   ["-p" "--password PASSWORD" "A temporary password for the user"]
                   ["-e" "--email EMAIL" "The user's email"]]
     :delegate put-user}]

   ["reset-password"
    {:description (:doc (meta #'reset-password))

     :cli-options [["-u" "--username USERNAME" "The username of the user"]
                   ["-p" "--password PASSWORD" "A temporary password for the user"]]
     :delegate reset-password}]

   ["put-role"
    {:description (:doc (meta #'put-role))
     :cli-options [["-r" "--role ROLE" "The role you are creating/updating"]
                   ["-d" "--description DESCRIPTION" "An optional description"]]
     :delegate put-role}]

   ["list-roles"
    {:description (:doc (meta #'list-roles))
     :cli-options []
     :delegate list-roles}]

   ["assign-role"
    {:description (:doc (meta #'assign-role))
     :cli-options [["-u" "--username USERNAME" "The user receiving the role"]
                   ["-r" "--role ROLE" "The role you are assigning"]
                   ["-j" "--justification JUSTIFICATION" "The business justification"]]
     :delegate assign-role}]

   ["put-rule"
    {:description (:doc (meta #'put-rule))
     :cli-options [["-n" "--name NAME" "Rule name"]
                   ["-r" "--rule FILE" "Rule file"]]
     :delegate put-rule}]

   ["put-trigger"
    {:description (:doc (meta #'put-trigger))
     :cli-options [["-n" "--name NAME" "Trigger name"]
                   ["-t" "--trigger FILE" "Trigger file"]]
     :delegate put-trigger}]

   ["put-graphql"
    {:description (:doc (meta #'put-graphql))
     :cli-options [["-f" "--file FILE" "GraphQL schema file"]
                   ["-p" "--path PATH" "The destination path after the base-uri"]]
     :delegate put-graphql}]

   ["post-graphql"
    {:description (:doc (meta #'post-graphql))
     :cli-options [["-f" "--file FILE" "GraphQL request"]
                   ["-p" "--path PATH" "The destination path after the base-uri"]]
     :delegate post-graphql}]

   ["put-redirect"
    {:description (:doc (meta #'put-redirect))
     :cli-options [["-r" "--resource URL" "Resource"]
                   ["-l" "--location URL" "Location to redirect to"]]
     :delegate put-redirect}]

   ["put-api"
    {:description (:doc (meta #'put-api))
     :cli-options [["-n" "--name NAME" "API name"]
                   ["-f" "--openapi FILE" "OpenAPI description file"]]
     :delegate put-api}]

   ["put-asset"
    {:description (:doc (meta #'put-asset))
     :cli-options [["-f" "--file FILE" "The asset file"]
                   ["-p" "--path PATH" "The destination path after the base-uri"]
                   ["-t" "--type MIME_TYPE" "The content-type"]
                   ["-e" "--encoding ENCODING" "The content-encoding (optional)"]
                   ["-l" "--language LANGUAGE" "The content-language (optional)"]
                   ["-r" "--resource URI" "The resource for which this asset is a variant of"]
                   ["-c" "--classification CLASSIFICATION" "The classification (e.g PUBLIC, RESTRICTED) applied to resource"]]
     :delegate put-asset}]

   ["put-template"
    {:description (:doc (meta #'put-template))
     :cli-options [["-f" "--file FILE" "The template file"]
                   ["-p" "--path PATH" "The destination path after the base-uri"]
                   ["-t" "--type MIME_TYPE" "The content-type"]
                   ["-e" "--encoding ENCODING" "The content-encoding (optional)"]
                   ["-l" "--language LANGUAGE" "The content-language (optional)"]
                   ["-d" "--dialect DIALECT" "The template dialect (e.g. selmer, mustache)"]]
     :delegate put-asset}]

   ["post-resources"
    {:description (:doc (meta #'post-resources))
     :cli-options [["-f" "--file FILE" "Resource file (in EDN format)"]]
     :delegate post-resources}]

   ["post"
    {:description (:doc (meta #'post))
     :cli-options
     [ ;; One of these
      ["-p" "--path PATH" "The destination path after the base-uri"]
      ["-u" "--url URL" "URL to post to"]

      ["-t" "--type MIME_TYPE" "The content-type"] ; If missing, try to deduce
                                                   ; from other the file suffix
      ["-e" "--encoding ENCODING" "The content-encoding (optional)"]
      ["-l" "--language LANGUAGE" "The content-language (optional)"]

      ;; One of these
      ["-d" "--data DATA" "Data to post"]
      ["-f" "--file FILE" "File containing data"]]
     :delegate post}]

   ["post-json"
    {:description (:doc (meta #'post-json))
     :cli-options [["-f" "--file FILE" "File containing data"]
                   ["-u" "--url URL" "URL to POST to"]]
     :delegate post-json}]

   ["put-json"
    {:description (:doc (meta #'put-json))
     :cli-options [["-f" "--file FILE" "File containing data"]
                   ["-u" "--url URL" "URL to POST to"]]
     :delegate put-json}]

   ["put-static-site"
    {:description (:doc (meta #'put-static-site))
     :cli-options [["-d" "--directory DIR" "Directory containing static site files"]
                   ["-p" "--path PATH" "Path the site will be available at. e.g -p mysite will make the site available at BASE_URI/mysite/index.html"]]
     :delegate put-static-site}]])

(let [{:keys [options arguments summary errors]}
      (parse-opts (next *command-line-args*)  global-cli-options :in-order true)
      command (first arguments)
      {:keys [cli-options delegate]} ((into {} commands) (first arguments))

      print-summary
      (fn []
        (do
          (println (str ansi/bold-green-font "site" ansi/reset-font))
          (println)
          (println "This is a resource management tool for Site.")
          (println)
          (println (str ansi/white-font "Usage" ansi/reset-font))
          (println)
          (println "site <global-options> <subcommand> <subcommand-options>")
          (println)
          (println (str ansi/white-font "Global options" ansi/reset-font))
          (println summary)
          (println)
          (println (str ansi/white-font "Environment variables" ansi/reset-font))
          (println)
          (println "SITE_BASE_URI \tSet the default Base-URI")
          (println "SITE_PASS_DIR \tOverride where Unix pass passwords are located")
          (println)
          (println (str ansi/white-font "Subcommands" ansi/reset-font))
          (println)
          (doseq [[command {:keys [description cli-options delegate]}] commands]
            (apply
             println
             (remove nil? [command (str ansi/yellow-font "(" description (when-not delegate " - coming soon!") ")" ansi/reset-font)]))
            (when-let [summary (:summary (parse-opts [] cli-options))]
              (when-not (str/blank? (str/trim summary))
                (println summary)))
            (println))))]

  (cond
    (contains? options :help)
    (print-summary)

    errors
    (doseq [err errors]
      (println err))

    (find (into {} commands) command)
    (binding [*silent* (:silent options)
              *debug* (:debug options)]
      (if delegate
        (let [opts (parse-opts (next arguments) (concat global-cli-options
                                                        cli-options))]
          (when-let [err (some->> opts :errors (str/join ", "))]
            (println (str ansi/red-font "Error: " err)))
          (delegate
           (-> opts
               (update :options merge options))))
        (println "No implementation:" command)))

    (first arguments)
    (println "Unknown command:" (first arguments))

    :else
    (print-summary)))

;; Local Variables:
;; mode: clojure
;; End:
